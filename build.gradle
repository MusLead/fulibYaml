plugins {
   id 'java'
   id 'maven-publish'
   // https://plugins.gradle.org/plugin/com.palantir.revapi
   id 'com.palantir.revapi' version '1.4.4'
   // https://plugins.gradle.org/plugin/com.bmuschko.nexus
   id 'com.bmuschko.nexus' version '2.3.1'
   // https://plugins.gradle.org/plugin/io.codearte.nexus-staging
   id 'io.codearte.nexus-staging' version '0.22.0'
}

group = 'org.fulib'
version = 'git describe --tags'.execute().text[1..-2] // strip v and \n
description = 'Yaml serialization for fulib object models. Used for storage and communication.'

modifyPom {
   project {
      name = project.name
      description = project.description
      url = 'https://github.com/fujaba/fulibYaml'
      inceptionYear = '2018'

      scm {
         url = 'https://github.com/fujaba/fulibYaml'
      }

      licenses {
         license {
            name = 'MIT License'
            url = 'http://www.opensource.org/licenses/mit-license.php'
         }
      }

      developers {
         developer {
            id = 'zuendorf'
            name = 'Albert Zuendorf'
         }
         developer {
            id = 'digitalhoax'
            name = 'Tobias George'
         }
         developer {
            id = 'eicke123'
            name = 'Christoph Eickhoff'
         }
         developer {
            id = 'clashsoft'
            name = 'Adrian Kunz'
         }
      }
   }
}

sourceCompatibility = 1.8

// --------------- Dependencies ---------------

repositories {
   mavenLocal()
   mavenCentral()
}

dependencies {
   // https://mvnrepository.com/artifact/org.hamcrest/hamcrest-library
   testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'

   // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
   testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.1'

   // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
   testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.1'
}

// --------------- Misc. Settings ---------------

test {
   useJUnitPlatform()
}

revapi {
   oldVersion = '1.4.0'
}
